def versionMajor = 0
def versionMinor = 1
def versionPatch = 0

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'


def isReleaseBuild = true
if(System.getenv("CARE_KEYSTORE") == null || System.getenv("CARE_KEYSTORE").equals("")){
    isReleaseBuild = false
}

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    signingConfigs {
        debug {
            keyAlias 'CuratedList'
            keyPassword 'curatedlist'
            storeFile file('build')
            storePassword 'curatedlist'
        }
        if(isReleaseBuild) {
            release {
                storeFile file(System.getenv("CL_KEYSTORE"))
                keyAlias System.getenv("CL_KEY_ALIAS")
                keyPassword System.getenv("CL_KEY_PASSWORD")
                storePassword System.getenv("CL_KEY_PASSWORD")
            }
        }
    }

    defaultConfig {
        applicationId "com.learncircle.geet.UI.curatedlist"
        minSdkVersion 16
        targetSdkVersion 24
        multiDexEnabled true
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources true
        }
        if(isReleaseBuild) {
            release {
                signingConfig signingConfigs.release
                minifyEnabled true
                shrinkResources true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile('com.android.support:appcompat-v7:24.2.1')
    compile('com.jakewharton:butterknife:8.4.0')
    apt('com.jakewharton:butterknife-compiler:8.4.0')
    compile('com.android.support:design:24.2.1')
    compile('com.firebase:firebase-client-android:2.5.2')
    compile('joda-time:joda-time:2.9.4')
    compile('com.android.support:palette-v7:24.2.1')
    compile('org.parceler:parceler-api:1.1.5')
    apt('org.parceler:parceler:1.1.5')
    compile('com.android.support:recyclerview-v7:24.2.1')
    compile('io.reactivex:rxandroid:1.2.1')
    compile('com.trello:rxlifecycle-android:0.8.0')
    compile('io.reactivex:rxjava:1.2.1')
    compile('com.android.support:support-vector-drawable:24.2.1')
    testCompile 'junit:junit:4.12'
    /* For Google Play Services */
    compile('com.google.android.gms:play-services-safetynet:8.3.0')
    compile('com.google.android.gms:play-services-auth:8.3.0')
    testCompile 'org.mockito:mockito-all:1.10.19'
    //Conflicting Dependencies resolved
    androidTestCompile('com.google.code.findbugs:jsr305:3.0.1')
}

